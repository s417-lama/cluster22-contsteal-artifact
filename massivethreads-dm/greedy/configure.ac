#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.
#
# 
# this file is initially generated by running autoscan
# in this directory and then modified to add necessary
# stuff
AC_PREREQ([2.69])
AC_INIT([madm], [0.1], [madm@eidos.ic.i.u-tokyo.ac.jp])
AC_CONFIG_AUX_DIR([misc/build-aux])
AC_CONFIG_MACRO_DIR([misc/m4])
AC_CONFIG_SRCDIR([comm/src/madm_comm.cc])
AC_CONFIG_HEADERS([comm/include/madm/madm_comm_acconfig.h])
AM_INIT_AUTOMAKE([foreign subdir-objects -Wall -Werror])

# avoid executing autotools on the user side
AM_MAINTAINER_MODE([disable])

# Checks for programs.

# default compiler flags
: ${CCFLAGS="-g -O3"}
: ${CXXFLAGS="-g -O3"}

# use mpic++ to compile everything
# non-standard commands (e.g., mpig++px) must be
# given via MPICC=mpig++px
AX_PROG_CXX_MPI([true])
AX_PROG_CC_MPI([true])

# install
AC_PROG_INSTALL

# ar
# added in reaction to:
# /usr/share/automake-1.14/am/ltlibrary.am: warning: 'libuth.la': linking libtool libraries using a non-POSIX
# /usr/share/automake-1.14/am/ltlibrary.am: archiver requires 'AM_PROG_AR' in 'configure.ac'
AM_PROG_AR

# assembler
# added for uni/context.S and iso/myth_context.S
AM_PROG_AS

#
# Checks for libraries.
#

# Checks for header files.
AC_CHECK_HEADERS([fcntl.h inttypes.h limits.h stddef.h stdint.h stdlib.h string.h sys/time.h unistd.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_CHECK_HEADER_STDBOOL
AC_C_INLINE
AC_TYPE_INT64_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_TYPE_UINT16_T
AC_TYPE_UINT32_T
AC_TYPE_UINT64_T
AC_TYPE_UINT8_T

# Checks for library functions.
AC_FUNC_ALLOCA
AC_FUNC_FORK
# this one ends up defining malloc as rpl_malloc and causes a compilation
# error with cstdlib (at a line containing ::malloc)
#AC_FUNC_MALLOC
AC_FUNC_MMAP
AC_CHECK_FUNCS([clock_gettime floor ftruncate gettimeofday memmove memset munmap pow select])
AC_SEARCH_LIBS([shm_open], [rt], [], [])


#
# custom options and checks
#

# -Wl,-no_pie option for Darwin to disable address space layout randomization
AC_CANONICAL_HOST

case $host_os in
  darwin*)
    LDFLAGS+=" -Wl,-no_pie"
  ;;
  linux*)
    LIBS+=" -lrt"
esac


#
# check if C++14 is supported
# The macro below automatically adds -std=gnu++14
#

AX_CXX_COMPILE_STDCXX(14, [ext], [mandatory])


#
# compiler flags
#
CFLAGS="$CFLAGS -fno-stack-protector"
CXXFLAGS="$CXXFLAGS -fno-stack-protector"

#
# --madi-debug-level=x --> -DMADI_DEBUG_LEVEL=x
#
AC_ARG_WITH([madi-debug-level],
            [AS_HELP_STRING([--with-madi-debug-level=n],
                            [Set debug level to n. Default: 0])],
            [madi_debug_level="$withval"],
            [madi_debug_level=0])

AC_DEFINE_UNQUOTED([MADI_DEBUG_LEVEL],[$madi_debug_level],[Debug Level])


#
# --with-comm-layer={seq,fx10}
# depending on the arg, define Makefile.am conditional
# COMM_LAYER_SHMEM and COMM_LAYER_FX10 to 0 or 1
# Makefile.am then add appropriate files to sources
#
AC_ARG_WITH([comm-layer],
            [AS_HELP_STRING([--with-comm-layer=c],
                            [Set comm layer to c. Default: shmem])],
            [comm_layer="MADI_COMM_LAYER_$(echo $withval | tr [[a-z]] [[A-Z]])"],
            [comm_layer="MADI_COMM_LAYER_SHMEM"])

# define MADI_COMM_LAYER MADI_COMM_LAYER_{SEQ,SHMEM,MPI3,GASNET,IBV,FX10}.
# The MPI3 implementation is experimental.
# AC_DEFINE_UNQUOTED([MADI_COMM_LAYER],[$comm_layer],[MADI comm layer])
# used in Makefile.am to add appropriate source files 
AM_CONDITIONAL([MADI_COMM_LAYER_SEQ],   [test "x$comm_layer" = "xMADI_COMM_LAYER_SEQ"])
AM_CONDITIONAL([MADI_COMM_LAYER_SHMEM], [test "x$comm_layer" = "xMADI_COMM_LAYER_SHMEM"])
AM_CONDITIONAL([MADI_COMM_LAYER_MPI3],  [test "x$comm_layer" = "xMADI_COMM_LAYER_MPI3"])
AM_CONDITIONAL([MADI_COMM_LAYER_GASNET],  [test "x$comm_layer" = "xMADI_COMM_LAYER_GASNET"])
AM_CONDITIONAL([MADI_COMM_LAYER_IBV],  [test "x$comm_layer" = "xMADI_COMM_LAYER_IBV"])
AM_CONDITIONAL([MADI_COMM_LAYER_FX10],  [test "x$comm_layer" = "xMADI_COMM_LAYER_FX10"])

AC_DEFINE_UNQUOTED([MADI_COMM_LAYER],[$comm_layer],[Communication Layer])

#
# polling
#
AC_ARG_ENABLE([polling],
              [AS_HELP_STRING([--enable-polling],
                              [Enable polling for communication progress. Default: no])],
              [case "${enableval}" in
                 yes) enable_polling=1 ;;
                 no)  enable_polling=0 ;;
               esac],
              [enable_polling=0])
AC_DEFINE_UNQUOTED([MADI_ENABLE_POLLING], [$enable_polling], [Enable polling])

#
# Logger
#
AC_ARG_ENABLE([logger],
              [AS_HELP_STRING([--enable-logger],
                              [Enable logger. Default: no])],
              [case "${enableval}" in
                 yes) enable_logger=1 ;;
                 no)  enable_logger=0 ;;
               esac],
              [enable_logger=0])
AC_DEFINE_UNQUOTED([MADI_ENABLE_LOGGER], [$enable_logger], [Enable logger])

AC_ARG_WITH([logger-enabled-kinds],
            [AS_HELP_STRING([--with-logger-enabled-kinds="KIND1 KIND2 ..."],
                            [List of logger kinds to be enabled for the logger. Default: ""])],
            [enabled_kinds=""
             for kind in $withval; do
               enabled_kinds+="kind::${kind},"
             done],
            [enabled_kinds=""])
AC_DEFINE_UNQUOTED([MADI_LOGGER_ENABLED_KINDS], [{${enabled_kinds}}], [Enabled kinds for logger])

AC_ARG_WITH([logger-disabled-kinds],
            [AS_HELP_STRING([--with-logger-disabled-kinds="KIND1 KIND2 ..."],
                            [List of logger kinds to be disabled for the logger. Default: ""])],
            [disabled_kinds=""
             for kind in $withval; do
               disabled_kinds+="kind::${kind},"
             done],
            [disabled_kinds=""])
AC_DEFINE_UNQUOTED([MADI_LOGGER_DISABLED_KINDS], [{${disabled_kinds}}], [Disabled kinds for logger])

#
# GASNet support
# --with-gasnet=path
# --with-gasnet-conduit=conduit
# --with-gasnet-thread={seq,par,parsync}
#
if test "x$comm_layer" = "xMADI_COMM_LAYER_GASNET"; then
  AC_ARG_WITH([gasnet],
              [AS_HELP_STRING([--with-gasnet=path],
                              [Set gasnet install directory to path.])],
              [gasnet_prefix=$withval],
              [gasnet_prefix="no"])

  AS_IF([test "x$gasnet_prefix" == xno],
        [AC_MSG_ERROR([Please specify -with-gasnet=path ($gasnet_prefix) option.])])

  AC_ARG_WITH([gasnet-conduit],
              [AS_HELP_STRING([--with-gasnet-conduit=conduit],
                              [Set gasnet conduit to conduit (default: mpi).])],
              [gasnet_conduit=$withval],
              [gasnet_conduit="mpi"])

  AS_IF([test "x$gasnet_conduit" != xmpi -a "x$gasnet_conduit" != xibv],
        [AC_MSG_ERROR([Please specify -with-gasnet-conduit={mpi,ibv}])])

  AC_ARG_WITH([gasnet-thread],
              [AS_HELP_STRING([--with-gasnet-thread=opt],
                              [Set gasnet thread option to opt (default: par).])],
              [gasnet_thread=$withval],
              [gasnet_thread="par"])

  AS_IF([test "x$gasnet_thread" != xseq -a "x$gasnet_thread" != xpar],
        [AC_MSG_ERROR([Please specify -with-gasnet-conduit={seq,par}.])])

  GASNET_THREAD_DEFINE="-DGASNET_$(echo $gasnet_thread | tr [[a-z]] [[A-Z]])"

  GASNET_CPPFLAGS="-I$gasnet_prefix/include"
  GASNET_CPPFLAGS="$GASNET_CPPFLAGS -I$gasnet_prefix/include/$gasnet_conduit-conduit"
  GASNET_CPPFLAGS="$GASNET_CPPFLAGS $GASNET_THREAD_DEFINE"
  GASNET_CPPFLAGS="$GASNET_CPPFLAGS -Wno-undef -Wno-unused-function -Wno-pragmas" # -Wno-deprecated-register -Wno-reserved-user-defined-literal"
  GASNET_LDFLAGS="-L$gasnet_prefix/lib -lgasnet-$gasnet_conduit-$gasnet_thread"

  AS_IF([test "x$gasnet_conduit" == xmpi],
        [GASNET_LDFLAGS="$GASNET_LDFLAGS -lammpi"])
  AS_IF([test "x$gasnet_conduit" == xibv],
        [GASNET_CPPFLAGS="$GASNET_CPPFLAGS -DGASNET_CONDUIT_IBV"])
  AS_IF([test "x$gasnet_conduit" == xibv],
        [GASNET_LDFLAGS="$GASNET_LDFLAGS -libverbs"])

  AS_IF([test "x$gasnet_prefix" != x],
        [CPPFLAGS="$CPPFLAGS $GASNET_CPPFLAGS"])
  AS_IF([test "x$gasnet_prefix" != x],
        [LDFLAGS="$LDFLAGS $GASNET_LDFLAGS"])
fi

#
# here we go
#
LT_INIT([disable-shared])
AC_CONFIG_FILES([Makefile
                 comm/Makefile
                 comm/include/Makefile
                 comm/include/madm/Makefile
                 comm/include/madm/seq/Makefile
                 comm/include/madm/shmem/Makefile
                 comm/include/madm/mpi3/Makefile
                 comm/include/madm/gasnet/Makefile
                 comm/include/madm/ibv/Makefile
                 comm/include/madm/fjmpi/Makefile
                 comm/include/mlog/Makefile
                 comm/src/Makefile
                 comm/examples/Makefile
                 comm/examples/one_sided/Makefile
                 comm/examples/perf/Makefile
                 uth/Makefile
		 uth/include/Makefile
		 uth/include/uth/Makefile
		 uth/include/uth/uni/Makefile
                 uth/src/Makefile
                 uth/examples/Makefile
                 uth/examples/plain/Makefile
                 uth/examples/host/Makefile
                 uth/examples/overhead/Makefile
                 uth/examples/flat/Makefile
                 uth/examples/bin/Makefile
                 uth/examples/bin_lambda/Makefile
                 uth/examples/uts/Makefile
                 uth/examples/nqueens/Makefile
                 misc/Makefile
                 misc/madmrun/Makefile
                 ])
AC_OUTPUT

